/*
 * Copyright Â© 2020 By Geeks Empire.
 *
 * Created by Elias Fazel
 * Last modified 5/15/20 9:30 AM
 *
 * Licensed Under MIT License.
 * https://opensource.org/licenses/MIT
 */

apply plugin: 'com.android.application'

apply plugin: 'com.google.firebase.crashlytics'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

apply plugin: 'com.google.gms.google-services'

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.3'

    defaultConfig {
        applicationId "net.geekstools.supershortcuts.PRO"

        minSdkVersion 26
        targetSdkVersion 29

        versionCode 197
        versionName "${majorVersion}." + getDateMonth() + "." + getDateDay() + "." + getDateYear() + ".${versionCode}"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'

            manifestPlaceholders = [enableCrashReporting: "true"]
        }
        debug {
            minifyEnabled false
            debuggable true

            manifestPlaceholders = [enableCrashReporting: "false"]
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    viewBinding {
        enabled = true
    }

    flavorDimensions "SuperShortcuts"
    productFlavors {
        productionConfiguration {
            dimension "SuperShortcuts"

            versionNameSuffix '[PRO][WearOS][Watch]'
        }
        betaConfiguration {
            dimension "SuperShortcuts"

            versionNameSuffix '[PRO][WearOS][Watch][BETA]'
        }
    }
}

static def getDateYear() {
    def date = new Date()
    def formattedDate = date.format('yyyy')//yyyyMMdd
    return formattedDate
}

static def getDateMonth() {
    def date = new Date()
    def formattedDate = date.format('MM')//yyyyMMdd
    return formattedDate
}

static def getDateDay() {
    def date = new Date()
    def formattedDate = date.format('dd')//yyyyMMdd
    return formattedDate
}

dependencies {
    implementation project(':LicenseLibrary')


    implementation 'androidx.wear:wear:1.0.0'
    compileOnly 'com.google.android.wearable:wearable:2.6.0'


    implementation 'com.google.android.support:wearable:2.6.0'
    implementation 'com.google.android.gms:play-services-wearable:17.0.0'


    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'

    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.preference:preference-ktx:1.1.1'


    implementation 'com.google.firebase:firebase-core:17.4.1'
    implementation 'com.google.firebase:firebase-analytics:17.4.1'

    implementation 'com.google.firebase:firebase-messaging:20.1.7'
    implementation 'com.google.firebase:firebase-config:19.1.4'

    implementation 'com.google.firebase:firebase-crashlytics:17.0.0'


    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.3'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.3'


    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}